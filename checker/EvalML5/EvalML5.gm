(*
  ML1: À°¿ô (represented by int) + »°Â§±é»» + ¿¿µ¶ÃÍ + If + Èæ³Ó±é»» 
  ML2: ML1 + let (´Ä¶­)
  ML3: ML2 + fun (´Ø¿ôÃê¾Ý¡¦Å¬ÍÑ) + let rec (ºÆµ¢´Ø¿ôÄêµÁ)
  ML4: ML3 + list + match
  ML5: ML4 + pattern matching
*)

[Syntax]

i in int

b in bool

x,y in Var

v in Value ::= i | b | Fun(env, x, e) | Rec(env, x, y, e) | NilV | ConsV(v,v)

env in Env ::= Empty
            |  Bind(env,x,v)

p in Pat ::= x | NilP | ConsP(p,p) | WildP (* wildcard pattern *)

res in Res ::= env | Fail (* result of matching *)

c in Clauses ::= SingleC(p,e) | AddC(p,e,c) (* stands for p -> e | c *)

e in Exp ::= i
          | b
          | x
          | BinOp(op, e, e)
          | If(e, e, e)
          | Let(x, e, e)
          | Abs(x, e)
          | App(e, e)
          | LetRec(x, y, e, e)  (* let rec x y = e in e *)
	  | Nil
	  | Cons(e,e)
	  | Match(e,c)   (* match e with p1 -> e1 | ... | pn -> en *)

op in Prim ::= Plus | Minus | Mult | Lt
  (* renamed due to conflicts with metavars for patterns *)

[Judgments]

Matches(v, p; res); (* The pattern p matches v according to assignment env *)
EvalTo(env, e; v);
AppBOp(op, v, v; v);

[Rules]

(* pattern matching *)
M-Var:  Matches(v, x, Bind(Empty,x,v)) :- ;
M-Nil:  Matches(NilV, NilP, Empty) :- ;
M-Cons: Matches(ConsV(v1, v2), ConsP(p1, p2), env) :-
           Matches(v1, p1, env1), Matches(v2, p2, env2),
            (*`disjoint $env1 $env2` %dom($env1)\cap dom($env2) = \emptyset%,*)
            `$env = uplus $env1 $env2` %$env = $env1 \uplus $env2%;
M-Wild: Matches(v, WildP, Empty) :- ;

(*
NM-IntNil:  Matches(i, NilP, Fail) :- ;
NM-BoolNil: Matches(b, NilP, Fail) :- ;
NM-FunNil:  Matches(Fun(env,x,e), NilP, Fail) :- ;
NM-RecNil:  Matches(Rec(env,x,y,e), NilP, Fail) :- ;
*)
NM-ConsNil: Matches(ConsV(v1,v2), NilP, Fail) :- ;
(*
NM-IntCons:  Matches(i, ConsP(p1,p2), Fail) :- ;
NM-BoolCons: Matches(b, ConsP(p1,p2), Fail) :- ;
NM-FunCons:  Matches(Fun(env,x,e), ConsP(p1,p2), Fail) :- ;
NM-RecCons:  Matches(Rec(env,x,y,e), ConsP(p1,p2), Fail) :- ;
*)
NM-NilCons: Matches(NilV, ConsP(p1,p2), Fail) :- ;

NM-ConsConsL: Matches(ConsV(v1,v2), ConsP(p1,p2), Fail) :- Matches(v1, p1, Fail);
NM-ConsConsR: Matches(ConsV(v1,v2), ConsP(p1,p2), Fail) :- Matches(v2, p2, Fail);


E-Int: 	EvalTo(env, i, i) :- ;
E-Bool: EvalTo(env, b, b) :- ;
E-IfT: 	EvalTo(env, If(e1, e2, e3), v) :- 
		EvalTo(env, e1, true), EvalTo(env, e2, v);
E-IfF: 	EvalTo(env, If(e1, e2, e3), v) :- 
		EvalTo(env, e1, false), EvalTo(env, e3, v);
E-Plus: EvalTo(env, BinOp(Plus, e1, e2), i3) :- 
		EvalTo(env, e1, i1), EvalTo(env, e2, i2), AppBOp(Plus, i1, i2, i3);
E-Minus: EvalTo(env, BinOp(Minus, e1, e2), i3) :- 
		EvalTo(env, e1, i1), EvalTo(env, e2, i2), AppBOp(Minus, i1, i2, i3);
E-Times: EvalTo(env, BinOp(Mult, e1, e2), i3) :- 
		EvalTo(env, e1, i1), EvalTo(env, e2, i2), AppBOp(Mult, i1, i2, i3);
E-Lt: 	EvalTo(env, BinOp(Lt, e1, e2), b3) :-
		EvalTo(env, e1, i1), EvalTo(env, e2, i2), AppBOp(Lt, i1, i2, b3);
(*
E-Var1:  EvalTo(Bind(env,x,v), x, v) :- ;
E-Var2:  EvalTo(Bind(env,y,v1), x, v2) :- `$y <> $x` %$y \neq $x%, EvalTo(env, x, v2);
*)
E-Var:  EvalTo(env, x, v) :- 
          `try $v = lookup $env $x with LookupFailure -> false`
          `$v = lookup $env $x`
          %$env($x) = $v%;
E-Let:  EvalTo(env, Let(x, e1, e2), v) :-
		EvalTo(env, e1, v1), EvalTo(Bind(env,x,v1), e2, v);
E-Fun: EvalTo(env, Abs(x, e), Fun(env, x, e)) :- ;
E-App: EvalTo(env, App(e1, e2), v) :-
         EvalTo(env, e1, Fun(env2, x, e0)),
         EvalTo(env, e2, v2),
         EvalTo(Bind(env2, x, v2), e0, v);

E-LetRec: EvalTo(env, LetRec(x, y, e1, e2), v) :-
                EvalTo(Bind(env,x,Rec(env,x,y,e1)), e2, v);
E-AppRec: EvalTo(env,App(e1, e2), v) :-
         EvalTo(env, e1, Rec(env2, x, y, e0)),
         EvalTo(env, e2, v2),
         EvalTo(Bind(Bind(env2, x, Rec(env2, x, y, e0)), y, v2), e0, v);
E-Nil: EvalTo(env,Nil,NilV) :- ;
E-Cons: EvalTo(env,Cons(e1,e2),ConsV(v1,v2)) :- EvalTo(env,e1,v1), EvalTo(env,e2,v2);
E-MatchM1: EvalTo(env,Match(e0,SingleC(p,e)),v') :- 
	    EvalTo(env,e0,v), Matches(v,p,env1), 
              `$env2 = uplus $env $env1` %$env2 = $env \uplus $env1%,
            EvalTo(env2,e,v');
E-MatchM2: EvalTo(env,Match(e0,AddC(p,e,c)),v') :- 
	    EvalTo(env,e0,v), Matches(v,p,env1), 
              `$env2 = uplus $env $env1` %$env2 = $env \uplus $env1%,
            EvalTo(env2,e,v');
E-MatchN: EvalTo(env,Match(e0,AddC(p,e,c)),v') :- 
	    EvalTo(env,e0,v), Matches(v,p,Fail), EvalTo(env,Match(e0,c),v');
           
B-Plus: AppBOp(Plus, i1, i2, i3) 	:- `$i3 = $i1 + $i2`;
B-Minus: AppBOp(Minus, i1, i2, i3) 	:- `$i3 = $i1 - $i2`;
B-Times: AppBOp(Mult, i1, i2, i3) 	:- `$i3 = $i1 * $i2`;
B-Lt: 	AppBOp(Lt, i1, i2, b3) 		:- `$b3 = ($i1 < $i2)`;

[ML]

exception LookupFailure

(*
let rec in_dom x = function
     Empty -> false
   | Bind(env, y, _) -> x = y || in_dom x env

let rec disjoint env1 env2 =
  match env1 with
     Empty -> true
   | Bind(env1, x, _) -> not (in_dom x env2) && disjoint env1 env2
*)

let rec uplus env1 env2 =
  (* disjointness of the two domains is guaranteed by parsing *)
  match env2 with
     Empty -> env1
   | Bind(env2, x, v) -> Bind(uplus env1 env2, x, v)

let rec lookup env x =
    match env with 
      Empty -> raise LookupFailure
    | Bind(env, x', v') -> if x = x' then v' else lookup env x
